/*
 * Copyright (C) 2020 Slack Technologies, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'com.slack.keeper'

android {
  compileSdkVersion 29

  defaultConfig {
    applicationId "com.slack.keeper.example"
    minSdkVersion 21
    targetSdkVersion 29
    versionCode 1
    versionName "1"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    testApplicationId "com.slack.keeper.sample.androidTest"
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
  }

  buildTypes {
    debug {
      matchingFallbacks = ['release']
    }
    release {
      minifyEnabled = true
      signingConfig = signingConfigs.debug
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.pro'
      testProguardFile('test-rules.pro')
      matchingFallbacks = ['release']
    }
    staging {
      initWith release
      debuggable true
    }
  }

  flavorDimensions "environment"
  productFlavors {
    internal {
      dimension "environment"
      applicationIdSuffix ".internal"
      versionNameSuffix "-internal"
    }

    external {
      dimension "environment"
    }
  }

  testBuildType = "staging"
}

keeper {
  // Example: Only enable on "externalStaging"
  variantFilter {
    setIgnore(name != "externalStaging")
  }

  // Example: emit extra debug information during Keeper's execution.
  emitDebugInformation = true

  // Example: automatic R8 repo management (more below)
  automaticR8RepoManagement = false
}

// Example demo of how to configure your own R8 repo
repositories {
  maven {
    url = uri("https://storage.googleapis.com/r8-releases/raw")
    content {
      includeModule("com.android.tools", "r8")
    }
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.70"
  implementation project(":sample-libraries:a")

  androidTestImplementation project(":sample-libraries:c")
  androidTestImplementation "com.squareup.okio:okio:2.4.3"
  androidTestImplementation "androidx.annotation:annotation:1.1.0"
  androidTestImplementation "androidx.test:rules:1.3.0-alpha03"
  androidTestImplementation "androidx.test:runner:1.3.0-alpha03"
  androidTestImplementation "androidx.test:orchestrator:1.3.0-alpha03"
  androidTestImplementation "androidx.test.ext:junit:1.1.2-alpha03"
  androidTestImplementation "junit:junit:4.13"
  androidTestImplementation "com.google.truth:truth:1.0.1"
}
