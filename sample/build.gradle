/*
 * Copyright (C) 2020 Slack Technologies, LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.android.build.api.variant.VariantBuilder
import com.android.build.gradle.internal.tasks.L8DexDesugarLibTask
import com.android.build.gradle.internal.tasks.R8Task
import com.slack.keeper.InferAndroidTestKeepRules
import com.slack.keeper.KeeperVariantMarker
import kotlin.PreconditionsKt

plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'com.slack.keeper'
}

android {
  compileSdk 33
  namespace = "com.slack.keeper.sample"

  defaultConfig {
    applicationId "com.slack.keeper.example"
    minSdk 21
    targetSdk 33
    versionCode 1
    versionName "1"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    testApplicationId "com.slack.keeper.sample.androidTest"
  }

  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    coreLibraryDesugaringEnabled = true
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
  }

  buildTypes {
    debug {
      matchingFallbacks += ['release']
    }
    release {
      minifyEnabled = true
      signingConfig = signingConfigs.debug
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.pro'
      testProguardFile('test-rules.pro')
      matchingFallbacks += ['release']
    }
    staging {
      initWith release
      debuggable true
    }
  }

  flavorDimensionList.add("environment")
  productFlavors {
    internal {
      dimension "environment"
      applicationIdSuffix ".internal"
      versionNameSuffix "-internal"
    }

    external {
      dimension "environment"
    }
  }

  testBuildType = "staging"
}

// Controlled by Keeper CI for testing
boolean useTraceReferences = providers.gradleProperty("keeperTest.enableTraceReferences")
    .map { Boolean.parseBoolean(it) }
    .orElse(false)

// Example: Only enable on "externalStaging"
androidComponents {
  beforeVariants(selector().all()) { VariantBuilder variantBuilder ->
    if (variantBuilder.name == "externalStaging") {
      variantBuilder.registerExtension(KeeperVariantMarker.class, KeeperVariantMarker.INSTANCE)
    }
  }
}

keeper {
  // Example: emit extra debug information during Keeper's execution.
  emitDebugInformation.set(true)

  // Example: automatic R8 repo management (more below)
  automaticR8RepoManagement.set(false)

  // Uncomment this line to debug the R8 from a remote instance.
  //r8JvmArgs.addAll(Arrays.asList("-Xdebug", "-Xrunjdwp:transport=dt_socket,address=5005,server=y,suspend=y"))

  if (useTraceReferences) {
    // Enable the new experimental TraceReferences tool.
    traceReferences {
      // Don't fail the build if missing definitions are found.
      arguments.set(["--map-diagnostics:MissingDefinitionsDiagnostic", "error", "info"])
    }
  }
}

// To speed up testing, we can also eagerly check that the generated rules match what we expect.
// This is _solely_ for CI use with Keeper!
boolean isCi = providers.environmentVariable("CI")
    .orElse("false")
    .get() == "true"
boolean verifyL8 = providers.gradleProperty("keeper.verifyL8")
    .orElse("false")
    .get() == "true"
if (isCi) {
  tasks.withType(InferAndroidTestKeepRules)
      .configureEach {
        doLast {
          println "Checking expected rules"
          String outputRules = outputProguardRules.getAsFile().get().text
          String expectedRules = file("expectedRules.pro").text
          if (outputRules != expectedRules) {
            throw new IllegalStateException("Rules don't match expected, existing ones:\n$outputRules")
          }
        }
      }

  if (verifyL8) {
    tasks.withType(L8DexDesugarLibTask)
        .matching { it.name == "l8DexDesugarLibExternalStagingAndroidTest" }
        .configureEach {
          doLast {
            println "Checking expected input rules from diagnostics output"
            String diagnosticFilePath = "build/intermediates/keeper/l8-diagnostics/l8DexDesugarLibExternalStagingAndroidTest/patchedL8Rules.pro"
            String diagnostics = file(diagnosticFilePath).text
            if (!diagnostics.contains("-dontobfuscate")) {
              throw new IllegalStateException("L8 diagnostic rules don't have Keeper's configured '-dontobfuscate', see ${diagnosticFilePath}")
            } else if (!diagnostics.contains("-keep class j\$.time.Instant")) {
              throw new IllegalStateException("L8 diagnostic rules include the main variant's R8-generated rules, see ${diagnosticFilePath}")
            }
          }
        }
  }

  tasks.withType(R8Task)
        .matching { it.name == "minifyExternalStagingWithR8" }
        .configureEach { r8Task ->
          doLast {
            println "Checking expected configuration contains embedded library rules from androidTest"
            def output = r8Task.getProguardConfigurationOutput()
            String allConfigurations = output.get().text
            logger.lifecycle("Verifying R8 configuration contents")
            if (!allConfigurations.contains("-keep class slack.test.only.Android { *; }")) {
              throw new IllegalStateException("R8 configuration doesn't contain embedded library rules from androidTest. Full contents:\n$allConfigurations")
            }
            if (!allConfigurations.contains("-keep class slack.test.only.Embedded { *; }")) {
              throw new IllegalStateException("R8 configuration doesn't contain embedded library rules from androidTest. Full contents:\n$allConfigurations")
            }
          }
        }
}

dependencies {
  implementation project(":sample-libraries:a")

  coreLibraryDesugaring libs.desugarJdkLibs

  androidTestImplementation project(":sample-libraries:c")
  androidTestImplementation libs.okio
  androidTestImplementation libs.androidx.annotation
  androidTestImplementation libs.androidx.test.rules
  androidTestImplementation libs.androidx.test.runner
  androidTestUtil libs.androidx.test.orchestrator
  androidTestImplementation libs.androidx.test.truth
  androidTestImplementation libs.junit
  androidTestImplementation libs.truth
  androidTestImplementation project(":sample-libraries:test-only-android")
  androidTestImplementation project(":sample-libraries:test-only-jvm")
}
