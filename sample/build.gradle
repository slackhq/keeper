/*
 * Copyright (C) 2020 Slack Technologies, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'com.slack.keeper'

android {
  compileSdkVersion 29

  defaultConfig {
    applicationId "com.slack.keeper.example"
    minSdkVersion 21
    targetSdkVersion 29
    versionCode 1
    versionName "1"
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
  }

  buildTypes {
    release {
      minifyEnabled = true
      signingConfig = signingConfigs.debug
      proguardFiles getDefaultProguardFile('proguard-android.txt')
    }
    staging {
      initWith release
      matchingFallbacks = ['release']
    }
  }

  flavorDimensions "environment"
  productFlavors {
    internal {
      dimension "environment"
      applicationIdSuffix ".internal"
      versionNameSuffix "-internal"
    }

    external {
      dimension "environment"
    }
  }

  testBuildType = "release"
}

keeper {
  automaticR8RepoManagement = false
  variantFilter {
    setIgnore(name == "externalRelease")
  }
}

repositories {
  maven {
    url = uri("https://storage.googleapis.com/r8-releases/raw")
    content {
      includeModule("com.android.tools", "r8")
    }
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.61"
  
  testImplementation "junit:junit:4.13"
  testImplementation "com.google.truth:truth:1.0.1"
}
