/*
 * Copyright (C) 2020 Slack Technologies, LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jetbrains.kotlin.gradle.plugin.KotlinBasePlugin

plugins {
    alias(libs.plugins.kotlin.jvm) apply false
    alias(libs.plugins.agp.library) apply false
    // Version just here to make gradle happy. It's always substituted as an included build
    id("com.slack.keeper") version "0.12.0" apply false
    alias(libs.plugins.spotless)
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    plugins.withType(KotlinBasePlugin.class).configureEach {
        project.tasks.withType(KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = "11"
                freeCompilerArgs += ['-progressive']
            }
        }
    }
}

spotless {
    format("misc") {
        target("**/*.md", ".gitignore")
        trimTrailingWhitespace()
        endWithNewline()
    }
    String ktlintVersion = "0.46.0"
    Map<String, String> ktlintUserData = ["indent_size": "2", "continuation_indent_size": "2"]
    kotlin {
        target("**/*.kt")
        targetExclude("**/.gradle/**", "**/build/**")
        ktlint(ktlintVersion).editorConfigOverride(ktlintUserData)
        trimTrailingWhitespace()
        endWithNewline()
        licenseHeaderFile(rootProject.file("spotless/copyright.kt"))
        targetExclude("**/copyright.kt")
    }
    kotlinGradle {
        target("**/*.kts")
        ktlint(ktlintVersion).editorConfigOverride(ktlintUserData)
        trimTrailingWhitespace()
        endWithNewline()
        licenseHeaderFile("spotless/copyright.kt", "(import|plugins|buildscript|dependencies|dependencyResolutionManagement|pluginManagement|rootProject)")
    }
    java {
        target("**/*.java")
        googleJavaFormat('1.14.0').reflowLongStrings()
        trimTrailingWhitespace()
        endWithNewline()
        licenseHeaderFile(rootProject.file("spotless/copyright.java"))
        targetExclude("**/copyright.java")
    }
}
